#pragma enable_d3d11_debug_symbols

// Reset
#pragma kernel ResetTextureKernel
#pragma kernel ResetAgentsKernel

// Step
#pragma kernel MoveAgentsKernel
#pragma kernel WriteTrailsKernel
#pragma kernel DiffuseTextureKernel

// Render
#pragma kernel RenderKernel

Texture2D<float4> readTex;
SamplerState sampler_readTex;

RWTexture2D<float4> writeTex;
RWTexture2D<float4> outTex;
RWTexture2D<float4> debugTex;

struct Agent
{
    float mass;
    float2 velocity;
    float2 acceleration;
    float2 position;
    float2 direction;
};

RWStructuredBuffer<Agent> agentsBuffer;

uint rez;
uint stepn;
uint time;
float trailDecayFactor;
int diffusionRange;
int sensorCount;
float sensorRange;
float sensorAngle;
float2 hitXY;
int brushSize;

// Physics
float mass;
float drag;
float speed;
float sensorForce;

// ------------------------------------------------------------
// UTIL
// ------------------------------------------------------------

// via "The Art of Code" on YouTube
float2 Random(float2 p) {
    float3 a = frac(p.xyx * float3(123.34, 234.34, 345.65));
    a += dot(a, a + 34.45);
    return frac(float2(a.x * a.y, a.y * a.z));
}

float2 RandomDirection(float2 p) {
    return (normalize(2.0 * (Random(p)- 0.5)));
}

float deg2rad(float angle){
    return angle * 3.14159265359 / 180;
}

float2 RotateAroundPoint(float2 p, float2 pivot, float angle) {
    
    // Convert to radians
    angle = deg2rad(angle);
    
    float s = sin(angle);
    float c = cos(angle);
    
    // translate point back to origin:
    p.x -= pivot.x;
    p.y -= pivot.y;
    
    // rotate point
    float xnew = p.x * c - p.y * s;
    float ynew = p.x * s + p.y * c;
    
    // translate point back:
    p.x = xnew + pivot.x;
    p.y = ynew + pivot.y;
    
    return p;
}

// ------------------------------------------------------------
// Physics Stuff
// ------------------------------------------------------------
Agent applyForce(Agent p, float2 force)
{
    force /= p.mass;
    p.acceleration += force;
    return p;
}

Agent applyDrag(Agent p, float coefficient)
{
    float speed = length(p.velocity);
    
    // The force’s magnitude
    float dragMagnitude = coefficient * speed * speed;
 
    float2 drag = p.velocity;
    drag *= -1;
    
    // The force's direction: -1 * velocity
    drag = normalize(drag);
 
    // Finalize the force: magnitude and direction together.
    drag *= dragMagnitude;
 
    // Apply the force.
    p = applyForce(p, drag);
    return p;
}

Agent attract(Agent p, float2 attractionPoint, float attractionStrength)
{
    float2 force = p.position - attractionPoint;
    float distance = length(force);
    force = normalize(force);
    float strength = (attractionStrength * p.mass) / (distance * distance);
    force *= strength;
    p = applyForce(p, -force);
    return p;
}

Agent repel(Agent p, float2 attractionPoint, float attractionStrength)
{
    float2 force = p.position - attractionPoint;
    float distance = length(force);
    force = normalize(force);
    float strength = (attractionStrength * p.mass) / (distance * distance);
    force *= strength;
    p = applyForce(p, force);
    return p;
}

// ------------------------------------------------------------
// RESET
// ------------------------------------------------------------

[numthreads(32, 32, 1)]
void ResetTextureKernel(uint3 id : SV_DispatchThreadID)
{
    writeTex[id.xy] = 0;
}

[numthreads(64, 1, 1)]
void ResetAgentsKernel(uint3 id : SV_DispatchThreadID)
{
    Agent a;
    
    a.position = Random(id.x * .0001 + time * .001) * rez;
    a.mass = mass;
    a.direction = RandomDirection(id.xx * .01 + sin(time));
    a.velocity = RandomDirection(id.xx * .01 + sin(time));
    a.acceleration = 0;
    
    agentsBuffer[id.x] = a;
}

// ------------------------------------------------------------
// STEP
// ------------------------------------------------------------
[numthreads(64, 1, 1)]
void WriteTrailsKernel(uint3 id : SV_DispatchThreadID)
{
    Agent a = agentsBuffer[id.x];
    writeTex[round(a.position)] = clamp(writeTex[round(a.position)] + .1, 0, 1);
}

// Turn based on surroundings
float2 SensorTurn(uint3 id, Agent a)
{   
    // Array of sensor positions
    float2 sensors[50];
    float angles[50];

    // For debug
    float4 red = float4(1,0,0,1);
    float4 green = float4(0,1,0,1);
    float4 blue = float4(0,0,1,1);
    
    // Position Sensors
    sensors[0] = a.position + a.direction * sensorRange;
    angles[0] = 0;
    for(int i = 1; i < sensorCount; i++){
        if(i % 2.0 == 1.0){
            sensors[i] = RotateAroundPoint(sensors[0], a.position, i * sensorAngle);
            angles[i] = i * sensorAngle;
        } else {
            sensors[i] = RotateAroundPoint(sensors[0], a.position, -1 * (i - 1) * sensorAngle);
            angles[i] = -1 * (i - 1) * sensorAngle;
        }
    }
    
    // Get Max Value
    float maxVal = 0;
    int bestSensor = 0;
    for(int j = 0; j < sensorCount; j++){
        if(distance(sensors[j], float2(0,0)) != 0){
            float level = readTex.SampleLevel(sampler_readTex, sensors[j] / (float)rez, 0).r;
            if(level > maxVal){
                maxVal = level;
                bestSensor = j;
            }
        }
        //debugTex[sensors[j]] = red;
    }
    
    //debugTex[sensors[bestSensor]] = green;
    
    // Output new direction
    float2 d = RotateAroundPoint(a.direction, float2(0,0), angles[bestSensor]);
    d = normalize(d);
    
    // Return new direction
    return d;
}

[numthreads(64, 1, 1)]
void MoveAgentsKernel(uint3 id : SV_DispatchThreadID)
{
    Agent a = agentsBuffer[id.x];
    
    // Basic Movement
    //a.direction = SensorTurn(id, a);
    //a.position = a.position + a.direction * speed;
    
    // Physics based movement
    a = applyForce(a, SensorTurn(id, a) * sensorForce);
    a = applyDrag(a, drag);
    a.velocity += a.acceleration;
    a.position += a.velocity * speed;
    a.acceleration *= 0;
    
    // Boundaries: Wrap
    if(a.position.x <= 0){
        a.position.x = rez - 1;
    }
    if(a.position.y <= 0){
        a.position.y = rez - 1;
    }
    if(a.position.x >= float(rez)){
        a.position.x = 1;
    }
    if(a.position.y >= float(rez)){
        a.position.y = 1;
    }
    
    if(stepn % 2 == 1) {
        agentsBuffer[id.x] = a;
    }
}

[numthreads(32, 32, 1)]
void DiffuseTextureKernel(uint3 id : SV_DispatchThreadID)
{
    float4 oc = readTex[id.xy];
    
    float avg = 0.0;
    int range = 1;
    int count = 0;
    for (int x = -range; x <= range; x++){
        for (int y = -range; y <= range; y++){
            float2 coord = (id.xy + int2(x,y)) / (float)rez;
            avg += readTex.SampleLevel(sampler_readTex, coord, 0).r;
            count++;
        }
    }
    
    avg /= count;
    
    oc = avg * trailDecayFactor;
    oc = clamp(oc, 0, 1);
    
    if((hitXY.x != 0 && hitXY.y != 0) && distance(hitXY, id.xy) < brushSize) {
        oc += 1;
    }
    
    writeTex[id.xy] = oc;
}

// ------------------------------------------------------------
// RENDER
// ------------------------------------------------------------

[numthreads(32, 32, 1)]
void RenderKernel(uint3 id : SV_DispatchThreadID)
{
    
    if((hitXY.x != 0 && hitXY.y != 0) && distance(hitXY, id.xy) < brushSize) {
        outTex[id.xy] = float4(1, 0, 0, 1);
    } else {
        outTex[id.xy] = readTex[id.xy];
    }
    outTex[id.xy] += debugTex[id.xy];
    debugTex[id.xy] = 0;
}